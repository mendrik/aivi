module tests.ui.html_sigil_and_runtime
export main

use aivi.testing (assert)
use aivi.text
use aivi.ui
use aivi.ui.layout

main : Effect Text Unit
main = effect {
  node =
    ~html~>
      <div class="card" style={ { width: 10px, gap: 2em } }>
        <span>{ TextNode "1" }</span>
      </div>
    <~html

  html = renderHtml node

  _ <- assert (contains "data-aivi-node=\"root\"" html)
  _ <- assert (contains "class=\"card\"" html)
  _ <- assert (contains "gap:" html)
  _ <- assert (contains "2em" html)
  _ <- assert (contains "width:" html)
  _ <- assert (contains "10px" html)
  _ <- assert (contains ">1</span>" html)

  old = ~html~> <div>{ TextNode "1" }</div> <~html
  new = ~html~> <div>{ TextNode "2" }</div> <~html
  ops = diff old new
  json = patchToJson ops

  _ <- assert (contains "\"op\":\"setText\"" json)
  _ <- assert (contains "\"id\":\"root/0\"" json)
  _ <- assert (contains "\"text\":\"2\"" json)

  click = eventFromJson "\{\"t\":\"click\",\"id\":42\}"
  okClick = click ?
    | Ok _  => True
    | Err _ => False
  _ <- assert okClick

  bad = eventFromJson "nope"
  okBad = bad ?
    | Ok _  => False
    | Err _ => True
  _ <- assert okBad

  pure Unit
}
