module examples.i18nSystemLocale

use aivi
use aivi.calendar (DateTime)
use aivi.file (open, readAll)
use aivi.i18n
use aivi.system (env)
use aivi.text (split)

defaultLocale : Locale
defaultLocale = { language: "en", region: Some "US", variants: [], tag: "en-US" }

systemLocale : Effect Text Locale
systemLocale = effect {
  lcAll <- env.get "LC_ALL"
  lcMsg <- env.get "LC_MESSAGES"
  lang  <- env.get "LANG"

  tagOpt =
    lcAll ?
      | Some t => Some t
      | None =>
        lcMsg ?
          | Some t => Some t
          | None   => lang

  tag =
    tagOpt ?
      | Some t => t
      | None   => defaultLocale.tag

  baseTag =
    (split "." tag) ?
      | [] => tag
      | [x, ..._] => x
  cleanTag =
    (split "@" baseTag) ?
      | [] => baseTag
      | [x, ..._] => x

  parseLocale cleanTag ?
    | Ok loc => pure loc
    | Err _  => pure defaultLocale
}

loadBundle : Locale -> Effect Text Bundle
loadBundle locale = effect {
  path =
    if locale.tag == "de-DE" then "examples/i18n/app.de-DE.properties" else "examples/i18n/app.en-US.properties"

  h <- open path
  propsRes <- readAll h

  propsRes ?
    | Ok props =>
      bundleFromProperties locale props ?
        | Ok b  => pure b
        | Err e => fail e
    | Err e => fail e
}

main : Effect Text Unit
main = effect {
  locale <- systemLocale
  bundle <- loadBundle locale

  _ <- print (t bundle (~k"app.welcome") { name: "Alice" })
  _ <- print (t bundle (~k"app.lastLogin") { when: ~dt(2026-02-12T15:30:00Z) })
  pure Unit
}
