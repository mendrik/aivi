module examples.compiler.classes
export Eq, Functor, Monad, Option, isNone

Option A = None | Some A

class Eq A = {
  eq: A -> A -> Bool
}

instance Eq (Bool) = {
  eq: x y => x == y
}

class Functor (F *) = {
  map: F A -> (A -> B) -> F B
}

instance Functor (Option *) = {
  map: opt f => opt ?
    | None   => None
    | Some x => Some (f x)
}

class Monad (M *) = Functor (M *) with {
  pure: A -> M A
  flatMap: M A -> (A -> M B) -> M B
}

instance Monad (Option *) = {
  map: opt f => opt ?
    | None   => None
    | Some x => Some (f x)

  pure: x => Some x

  flatMap: opt f => opt ?
    | None   => None
    | Some x => f x
}

instance Eq (Option Bool) = {
  eq: a b => (a, b) ?
    | (None, None)     => True
    | (Some x, Some y) => x == y
    | _                => False
}

isNone = opt => opt ?
  | None   => True
  | Some _ => False
