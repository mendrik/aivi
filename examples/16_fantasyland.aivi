module examples.fantasy_land
export Option, isNone

class Setoid A = {
  equals: A -> A -> Bool
}

class Functor (F *) = {
  map: (A -> B) -> F A -> F B
}

class Apply (F *) = {
  ap: F (A -> B) -> F A -> F B
}

class Applicative (F *) = {
  of: A -> F A
}

class Chain (F *) = {
  chain: (A -> F B) -> F A -> F B
}

Option A = None
| Some A

instance Setoid (Option A) = {
  equals: a b => (a, b) ?
  | (None, None)     => True
  | (Some x, Some y) => equals x y
  | _                => False
}

instance Functor (Option *) = {
  map: f opt => opt ?
  | None   => None
  | Some x => Some (f x)
}

instance Apply (Option *) = {
  ap: f_opt opt => (f_opt, opt) ?
  | (Some f, Some x) => Some (f x)
  | _                => None
}

instance Applicative (Option *) = {
  of: x => Some x
}

instance Chain (Option *) = {
  chain: f opt => opt ?
  | None   => None
  | Some x => f x
}

isNone = opt => opt ?
| None   => True
| Some _ => False
